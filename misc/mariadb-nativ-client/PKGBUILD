# Maintainer: Jan Was <janek.jan@gmail.com>
# Modification: EnteEnteEnte <ducksource@duckpond.ch>
# vim:set ts=2 sw=2 et:

pkgname="mariadb-native-client"
pkgver="2.0.0"
pkgrel=1
pkgdesc="LGPL MariaDB client library"
arch=('i686' 'x86_64')
url="http://mariadb.org"
license=('LGPL')
depends=()
makedepends=('cmake' 'openssl' 'zlib')
optdepends=()

_TARBALL_NAME="mariadb_client-${pkgver}"

install="${pkgname}.install"
source=("${_TARBALL_NAME}::https://downloads.mariadb.org/f/client-native-2.0.0/src/mariadb_client-2.0.0-src.tar.gz/from/http%3A/tweedo.com/mirror/mariadb?serve",
        'mariadb-native.conf')
sha256sums=('fd2f751dea049c1907735eb236aeace1d811d6a8218118b00bbaa9b84dc5cd60'
            'e13627b886af7aeb85666ef2a55e437892c106b1c6f14813d892d54bbf875939')

build() {
  cd "$srcdir"

  if [ ! -e build ]; then
    mkdir -p build
  fi
  cd build

  cmake "../${_TARBALL_NAME}-src" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"
  make -C 'build/' DESTDIR="$pkgdir" install

  install -Dm 644 'mariadb-native.conf' "${pkgdir}/etc/ld.so.conf.d/mariadb-native.conf"
}

